buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-jetty:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1211'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '4.1.2'
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude module: "spring-boot-starter-tomcat"
    }

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}

task startApp(type: Exec, dependsOn: ["bootRepackage"]) {
    workingDir ".."
    commandLine 'docker-compose', '-f', 'docker-compose-local.yml', 'up', '--force-recreate'
}

task stopApp(type: Exec) {
    workingDir ".."
    commandLine 'docker-compose', '-f', 'docker-compose-local.yml', 'down', '--rmi', 'local'
}

task startIntegrationDatabase(type: Exec) {
    commandLine "docker", "run", "--rm", "-it", "-p", "5432:5432", "--name", "opendoor-postgres-test", "-e", "POSTGRES_PASSWORD=mysecretpassword", "-d", "postgres:alpine"
}

task stopIntegrationDatabase(type: Exec) {
    commandLine "docker", "rm", "-f", "opendoor-postgres-test"
}

test.dependsOn('startIntegrationDatabase')
test.finalizedBy stopIntegrationDatabase

apply from: "${rootDir}/build-support/docker_publish.gradle"
